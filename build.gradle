buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}

plugins {
    // Apply the java-library plugin to add support for Java Library
    id 'java-library'

    id 'maven-publish'
}

apply plugin: 'io.codearte.nexus-staging'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

repositories {
    jcenter()
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:23.0'

    // Test frameworks required for testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'

}

allprojects {
    apply plugin: 'signing'
    apply plugin: 'maven'

    // Signature of artifacts
    signing {
        sign configurations.archives
    }

    // OSSRH publication
    uploadArchives {
        repositories {
            mavenDeployer {
                // POM signature
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                // Target repository
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }
                pom.project {
                    groupId 'zw.co.paynow'
                    artifactId 'java-sdk'
                    version '1.1.0'
                    name 'java-sdk'
                    description 'This project contains libraries to interface with Zimbabwe\'s Leading Payments Gateway Paynow'
                    packaging 'jar'
                    url 'https://github.com/paynow/Paynow-Java-SDK'
                    inceptionYear '2018'

                    scm {
                        connection 'scm:git:https://github.com/paynow/Paynow-Java-SDK.git'
                        developerConnection 'scm:git:git@github.com:paynow/Paynow-Java-SDK.git'
                        url 'https://github.com/paynow/Paynow-Java-SDK.git'
                    }

                    licenses {
                        license {
                            name 'GNU General Public License v3.0'
                            url 'https://www.gnu.org/licenses/gpl.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'paynow'
                            name = 'Webdev Projects'
                            email = 'projects@webdevworld.com'
                        }
                    }
                }
            }
        }
    }
}

nexusStaging {
    username = nexusUsername
    password = nexusPassword
}