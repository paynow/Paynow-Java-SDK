buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id("io.github.gradle-nexus.publish-plugin") version "2.0.0"
}

java {
    withJavadocJar() // Automatically adds the Javadoc JAR to the publication
    withSourcesJar() // Automatically adds the Sources JAR to the publication
}

repositories {
    mavenCentral()
}

dependencies {
    api 'org.apache.commons:commons-math3:3.6.1'
    implementation 'com.google.guava:guava:32.0.1-jre'

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.mockito:mockito-core:5.15.2'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
}

test {
    finalizedBy jacocoTestReport
    useJUnitPlatform()
    testLogging {
        events = ["passed", "failed", "skipped"]
    }
}

jacocoTestReport {
    reports {
        xml.required.set(true)
        html.required.set(false)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'zw.co.paynow'
            artifactId = 'java-sdk'
            version = '1.1.2'

            from components.java // Automatically includes the main JAR, Sources JAR, and Javadoc JAR

            pom {
                name = 'java-sdk'
                description = 'This project contains libraries to interface with Zimbabwe\'s Leading Payments Gateway Paynow'
                url = 'https://github.com/paynow/Paynow-Java-SDK'
                inceptionYear = '2018'

                scm {
                    connection = 'scm:git:https://github.com/paynow/Paynow-Java-SDK.git'
                    developerConnection = 'scm:git:git@github.com:paynow/Paynow-Java-SDK.git'
                    url = 'https://github.com/paynow/Paynow-Java-SDK.git'
                }

                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'paynow'
                        name = 'Paynow'
                        email = 'developers@paynow.co.zw'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = nexusUsername
                password = nexusPassword
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava // Sign the `mavenJava` publication
}
